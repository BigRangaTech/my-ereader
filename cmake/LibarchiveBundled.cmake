if (USE_BUNDLED_LIBARCHIVE)
  if (ANDROID)
    set(LIBARCHIVE_BUNDLED_ROOT "${CMAKE_SOURCE_DIR}/third_party/install/android/${ANDROID_ABI}/libarchive")
  else()
    set(LIBARCHIVE_BUNDLED_ROOT "${CMAKE_SOURCE_DIR}/third_party/install/libarchive")
  endif()
endif()

set(LIBARCHIVE_AVAILABLE FALSE)

if (USE_BUNDLED_LIBARCHIVE)
  set(_libarchive_inc "${LIBARCHIVE_BUNDLED_ROOT}/include")
  set(_libarchive_lib "${LIBARCHIVE_BUNDLED_ROOT}/lib/libarchive.so")
  if (EXISTS "${_libarchive_inc}" AND EXISTS "${_libarchive_lib}")
    add_library(Archive::Archive INTERFACE IMPORTED)
    target_include_directories(Archive::Archive INTERFACE "${_libarchive_inc}")
    target_link_libraries(Archive::Archive INTERFACE "${_libarchive_lib}")
    set(LIBARCHIVE_AVAILABLE TRUE)
    add_compile_definitions(HAVE_LIBARCHIVE=1)
  endif()
endif()

if (NOT LIBARCHIVE_AVAILABLE)
  find_package(LibArchive QUIET)
  if (LibArchive_FOUND)
    if (TARGET LibArchive::LibArchive)
      add_library(Archive::Archive INTERFACE IMPORTED)
      target_link_libraries(Archive::Archive INTERFACE LibArchive::LibArchive)
    endif()
    set(LIBARCHIVE_AVAILABLE TRUE)
    add_compile_definitions(HAVE_LIBARCHIVE=1)
  endif()
endif()

if (NOT LIBARCHIVE_AVAILABLE)
  find_package(PkgConfig QUIET)
  if (PkgConfig_FOUND)
    pkg_check_modules(LIBARCHIVE QUIET libarchive)
    if (LIBARCHIVE_FOUND)
      add_library(Archive::Archive INTERFACE IMPORTED)
      target_include_directories(Archive::Archive INTERFACE ${LIBARCHIVE_INCLUDE_DIRS})
      target_link_libraries(Archive::Archive INTERFACE ${LIBARCHIVE_LIBRARIES})
      set(LIBARCHIVE_AVAILABLE TRUE)
      add_compile_definitions(HAVE_LIBARCHIVE=1)
    endif()
  endif()
endif()
